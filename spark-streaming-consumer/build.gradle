buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

apply plugin: 'docker'
apply plugin: 'idea'

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

mainClassName = 'me.loki2302.App'

version = '1.0'
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    // ./gradlew clean shadowJar

    // this one is not included to the uber jar
    provided 'org.apache.spark:spark-streaming_2.10:1.3.1'
    provided 'com.fasterxml.jackson.core:jackson-databind:2.4.4'

    // this one is
    compile 'org.apache.spark:spark-streaming-kafka_2.10:1.3.1'
    compile 'commons-cli:commons-cli:1.2'
    compile 'org.springframework.boot:spring-boot-starter:1.2.3.RELEASE'
    compile 'org.springframework:spring-web:4.1.6.RELEASE'
}

task dockerContainer(type: Docker) {
    applicationName = 'streaming-consumer'
    tagVersion = '1.0'

    dockerfile = file('Dockerfile')
    addFile "${buildDir}/libs/spark-streaming-consumer-1.0-all.jar", '/'
}
dockerContainer.dependsOn 'shadowJar'

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.provided ]
    }
}