buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

apply plugin: 'docker'

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

mainClassName = 'me.loki2302.App'

version = '1.0'
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    // ./gradlew clean shadowJar

    // this one is not included to the uber jar
    provided 'org.apache.spark:spark-streaming_2.10:1.3.1'

    // this one is
    compile 'org.apache.spark:spark-streaming-kafka_2.10:1.3.1'
}

task dockerContainer(type: Docker) {
    applicationName = 'streaming-consumer'
    tagVersion = '1.0'

    baseImage = 'loki2302/sparkbase:latest'
    addFile "${buildDir}/libs/spark-streaming-consumer-1.0-all.jar", '/'
    workingDir '/opt/spark-1.3.1-bin-hadoop2.6'
    defaultCommand = [
            './bin/spark-submit',
            '/spark-streaming-consumer-1.0-all.jar',
            '--master', 'spark://sparkmaster:7077']
}
dockerContainer.dependsOn 'shadowJar'
